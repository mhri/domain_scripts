! version = 2.0

+ e:person-appeared
//+ 안녕
- 피부에 대한 고민이 있거나 상담이 필요하시면 가까이 오세요. 제가 도와 드릴께요.{topic=t_scenariostart}
- 방문해 주셔서 감사합니다. 피부관리에 필요한 모든 것을 상담해 드립니다.{topic=t_scenariostart}
- 피부에 대한 상태를 말씀해 주시면 자세히 상담해 드리겠습니다.{topic=t_scenariostart}


> topic exception_person_disappeared
    + e:person-disappeared
    - <overriding=2> 제 앞에서 벗어나셨군요. 다음에 다시 와주세요. {topic=reset}
< topic

> topic exception_person_disengaged
    + e:person-disengaged
    - 상담이 지루하시더라도 집중해 주시면 감사하겠습니다.
    - 노잼이어도 알아두시면 괜찮은 정보입니다. 집중해주세요.
< topic

> topic t_scenariostart
    //+ e:face-detected
    + (e:person-engaged|안녕)
    - {topic=getperson}{@start}
< topic

> topic getperson
    + start
    - <set face_id=<call>mget person_detection/person_id[0]</call>> {topic=greeting}{@start}
< topic


> topic greeting
    + start
    - <br=2> <sm=tag:greeting[happiness]> 만나서 반갑습니다. 저는 스킨케어 로봇입니다. {@next}
    - <br=2> <sm=tag:greeting[happiness]> 안녕하세요? 만나서 반갑습니다. 저는 스킨케어 로봇입니다. {@next}
    - <br=2> <sm=tag:greeting[happiness]> 만나서 반갑습니다. 저는 스킨케어 로봇입니다. 고객님에게 적합한 제품을 추천해드립니다. {@next}

    //+ e:person-engaged //이벤트
    + next
    //- <set name=<call>mget face_recognition/name[session_face_id:face_id]</call>> 어디 보자, 방문한 적이 있는지 알아볼게요.{topic=greeting2}{@start}
    - 어디 보자, 방문한 적이 있는지 알아볼게요. {topic=getname_first}{@start}
    - 방문한 적이 있으셨나요? {topic=getname_first}{@start}
< topic

> topic getname_first
    + start
    - <set name=<call>mget person_identification/name[session_face_id:face_id]</call>> {topic=greeting2}{@start}
< topic

> topic greeting2
    + start
    * <get name> == undefined => <br=2> 저는 기억이 없는데, 이름을 물어봐도 될까요? <set first_visit=1> {topic=getname}
    * <get name> != undefined => <br=2> <get name>님, 지난 번에 오신적이 있네요. 다시 방문해 주셔서 감사합니다. <set first_visit=0> {topic=t_personal}{@start}
< topic


> topic getname
    + (내 이름은 *|내이름은 *|나는 *라고 해)
    * <star4> != None => <set name=<star4>> {topic=greeting3}{@start}
    * <star3> != None => <set name=<star3>> {topic=greeting3}{@start}
    * <star2> != None => <set name=<star2>> {topic=greeting3}{@start}
< topic

> topic greeting3
    + start
    - <get name>님 반갑습니다. 다음 번에 방문하시면 제가 먼저 인사드릴께요. {topic=t_personal}{@start}
    - <get name>님 반갑습니다. 기억하겠습니다. {topic=t_personal}{@start}
    - <get name>님 반갑습니다. 기억할께요. {topic=t_personal}{@start}
< topic


> topic t_personal
    + start
    - <br=2> 피부와 상관은 없지만, 상담을 시작하기 전에 제가 <get name>님의 모습을 좀 살펴 볼께요.{topic=t_personal_d}{@start}
< topic

> topic t_personal_d
    + start
    - <br=2> 저를 잠시 보세요. {@gender_detect}

    + gender_detect
    * <call>mget person_identification/gender[session_face_id:face_id]</call> == unknown => 제가 성별을 잘 못알아보겠네요. 남성이신가요? 여성이신가요? {topic=t_gender}
    * <call>mget person_identification/gender[session_face_id:face_id]</call> == male => 멋진 남성분이시군요. <set gender=남성> {@age_detect}
    * <call>mget person_identification/gender[session_face_id:face_id]</call> == female => 아름다운 여성분이시군요. <set gender=여성> {@age_detect}

    + age_detect // 메모리 읽어오기
    * <call>mget person_identification/age[session_face_id:face_id]</call> >= 40 => 40세보다 많으십니다. <set age=나이가 있으> {@identify}
    * <call>mget person_identification/age[session_face_id:face_id]</call> < 40 => 40세보다 적으십니다. <set age=젋으> {@identify}
    - 제가 눈이 안좋은지 연령대를 추측할수가 없네요. 연세가 어떻게 되시나요? {topic=t_ageaaa}

    + identify
   	//* <call>mget person_identification/hair_style[session_face_id:face_id]</call> == long => 긴 머리가 아주 잘어울려요. {@next}
   	//* <call>mget person_identification/hair_style[session_face_id:face_id]</call> == short => 시원하게 짧은 머리가 아주 잘어울려요.{@next}
    * <call>mget person_identification/cloth_color[session_face_id:face_id]</call> == Red => 빨간옷이 따뜻해 보이네요.{@next}
    * <call>mget person_identification/cloth_color[session_face_id:face_id]</call> == Blue => 파란색옷이 잘 어울리네요.{@next}
    * <call>mget person_identification/cloth_color[session_face_id:face_id]</call> == Green => 녹색옷이 상큼해 보이네요. {@next}
    * <call>mget person_identification/cloth_color[session_face_id:face_id]</call> == Black => 검정색 옷이 차분해 보이네요. {@next}
    * <call>mget person_identification/cloth_color[session_face_id:face_id]</call> == Purple => 보라색옷이 아주 멋있어 보여요. {@next}
    * <call>mget person_identification/cloth_color[session_face_id:face_id]</call> == Brown => 갈색옷이 아주 멋있어 보여요. {@next}
    * <call>mget person_identification/cloth_color[session_face_id:face_id]</call> == Gray => 회색 옷이 아주 멋있어 보여요. {@next}
    * <call>mget person_identification/cloth_color[session_face_id:face_id]</call> == Orange => 오렌지색 옷이 아주 멋있어 보여요. {@next}
    * <call>mget person_identification/cloth_color[session_face_id:face_id]</call> == Pink => 핑크색 옷이 아주 멋있어 보여요. {@next}
    * <call>mget person_identification/cloth_color[session_face_id:face_id]</call> == White => 흰 옷이 아주 멋있어 보여요. {@next}
    * <call>mget person_identification/cloth_color[session_face_id:face_id]</call> == Yellow => 노란색옷이 아주 멋있어 보여요. {@next}
    //* <call>mget person_identification/cloth_color[session_face_id:face_id]</call> == '' => 좀 더 가까이 와 주세요....{@next}
    - 스타일이 괜찮으시네요. {@next}

    + next
    - {topic=t_start}{@start}
< topic

> topic t_gender inherits exception_person_disengaged exception_person_disappeared
    + (남자|남성)
    - <set gender=남성> 그렇군요. 알려주셔서 감사합니다. {topic=t_personal_d}{@age_detect}

    + (여자|여성)
    - <set gender=여성> 아! 그렇군요. 아름다우신데 못 알아봐서 죄송합니다. {topic=t_personal_d}{@age_detect}
< topic

> topic t_ageaaa inherits exception_person_disengaged exception_person_disappeared
    + #
    * <star> > 40 => <set age=나이가 있으> 외모보다 나이가 많으시네요? {topic=t_personal_d}{@identify}
    * <star> < 40 => <set age=젋으> 역시 제 예상대로 젊으시군요? {topic=t_personal_d}{@identify}
    - 다시 한번 말씀해주세요.
< topic

> topic t_start
    + start
    * <get first_visit> == 0 => <br=5> 먼저 유전자 검사 결과를 살펴보도록 하겠습니다. 잠시만요. {topic=t_genom_explain}{@start}
    * <get first_visit> == 1 => <br=5> 유전자 검사를 먼저 받으셔야 자세한 상담이 가능합니다.  {topic=reset}
    //-
< topic

> topic t_genom_explain
    + start
    - <call>get_genom_data</call> <br=2> 내용이 좀 긴데 말씀 드릴까요? {topic=t_genom}

< topic

> topic t_genom inherits exception_person_disengaged exception_person_disappeared
    + 그래
    - 고객님의 유전자 검사 결과를 살펴보겠습니다. \n
    ^ 피부탄력을 결정하는 MMP1 유전자는 세포 속 콜라겐 함유량이 평균량 <get genom_skin> 으로 나타났습니다.\n
    ^ 색소침착을 결정하는 OCA2, MC1R 유전자는 멜라노사이트에 의해 색소 누적이 <get genom_color> 것으로 보여집니다.\n
    ^ 피부 노화는 콜라겐의 양을 보니 피부 재생 속도가 <get genom_old> 것으로 보입니다.\n
    ^ 마지막으로 비타민C를 흡수하는 능력이 <get genom_vitamin> 것으로 보입니다. {@next}

    + 아니
    - {@next}

    + next
    - <br=3> 자, 그럼 다음 단계로 넘어가도록 하겠습니다. 고객님의 정보를 몇가지 알려주시면 제가 고객님께 알맞은 화장품을 추천해드리도록 하겠습니다. {topic=t_skin_state}{@start}
< topic



> topic t_skin_state
    + start
    - <get name>님의 피부 상태에 대해 말씀해 주세요. 건성, 지성 중 어떤 피부이신가요? {topic=t_skin_state_answer}
    - <gaze=person:<get face_id>> 어디 보자, 건성인가? 지성인가? {topic=t_skin_state_answer}
< topic

> topic t_skin_state_answer inherits exception_person_disengaged exception_person_disappeared
    + 건성
    - <set skin_state=건성> 피부가 건조하시군요! {topic=t_price_range}{@start}

    + 지성
    - <set skin_state=지성> 평소 유분 관리에 신경이 쓰이시겠어요. {topic=t_price_range}{@start}

    + (e:turn-release-detected|e:silency-detected)
    - <get name>님, 생각을 너무 오래 하시네요. 건성과 지성 중 선택해서 말씀해주세요.
    - <get name>님, 고민 중 이신가요? <get name>님의 피부를 한번 살펴보시고, 건성과 지성 중 어떤게 맞는지 대답해주세요.

    //+ *
    //- 그럼, 대부분의 사람들이 지성이니 그렇게 알도록 하겠습니다. 다음으로 진행할까요?

    //+ 그래
    //% 그럼 대부분의 사람들이 지성이니 그렇게 알도록 하겠습니다 다음으로 진행할까요
    //- 알겠습니다. {topic=t_price_range}{@start}

    //+ 아니
    //% 그럼 대부분의 사람들이 지성이니 그렇게 알도록 하겠습니다 다음으로 진행할까요
    //- 건성과 지성 중 어떤 피부이신가요?
< topic



> topic t_price_range
    + start
    - <get name>님, 가격대가 좀 높지만 효과가 확실한 제품을 원하시나요? {topic=t_price_range_answer}
< topic

> topic t_price_range_answer inherits exception_person_disengaged exception_person_disappeared
    + 그래
    - <set price_range=비싼> 선택하신 것이 후회되지 않은 좋은 제품을 알려 드릴께요. {topic=t_select_item}{@start}

    + (아니|저렴*)
    - <set price_range=저렴한> 음.. 최선을 다해 제품을 알아보겠습니다. {topic=t_select_item}{@start}
< topic



> topic t_select_item
    + start
    - <br=2> 지금까지 제가 얻은 정보를 종합해 보면, <get name>님은 <get gender>이시고, 피부 상태는 <get skin_state>, 연령은 <get age>신 것으로 되어 있습니다. \n
    ^ 그리고, 가격은 <get price_range> 제품으로 선택하셨구요. {@start2}

    + start2
    - <br=2> 알맞는 제품을 검색 중입니다. {@start3}

    + start3
    //- <delay=5> 검색이 완료되었습니다. <get name>님께는 <call>select_item_type</call>을 추천해 드립니다. 좀더 자세히 알려드릴까요?
    - <br=5> 검색이 완료되었습니다. <get name>님께는 <call>select_item_type</call>을 추천해 드립니다. 좀더 자세히 알려드릴까요? {topic=t_agree_detail_tem}
< topic

> topic t_agree_detail_tem inherits exception_person_disengaged exception_person_disappeared
    + 그래
    - 어떤 제품으로 알려드릴까요? {topic=t_detailed_item}

    + 아니
    - 그럼 처음으로 돌아갑니다. {topic=reset}
< topic


> topic t_detailed_item inherits exception_person_disengaged exception_person_disappeared
    + 스킨
    * <get skin> != undefined => <gaze=objects:Box1><pointing=objects:Box1> 스킨 제품은 이쪽에 있습니다. <call>get_description_item skin</call> {@next}
    - 제가 추천해드린 제품 중 스킨은 해당사항이 없습니다. 추천된 제품 중 하나를 말씀해주세요.

    + 썬크림
    * <get suncream> != undefined => <gaze=objects:Box1><pointing=objects:Box2> 썬크림 제품은 이쪽을 보세요. <call>get_description_item suncream</call> {@next}
    - 제가 추천해드린 제품 중 썬크림은 해당사항이 없습니다. 추천된 제품 중 하나를 말씀해주세요.

    + 에센스
    * <get essense> != undefined => <gaze=objects:Box1><pointing=objects:Box1> 에센스는 이쪽에 모여져 있습니다. <call>get_description_item essense</call> {@next}
    - 제가 추천해드린 제품 중 에센스는 해당사항이 없습니다. 추천된 제품 중 하나를 말씀해주세요.

    + 크림
    * <get cream> != undefined => <gaze=objects:Box1><pointing=objects:Box1> 크림은 이쪽을 보세요. <call>get_description_item cream</call> {@next}
    - 제가 추천해드린 제품 중 크림은 해당사항이 없습니다. 추천된 제품 중 하나를 말씀해주세요.

    + (싫어|그만|됐어|아니|싫 어)
    - 네. 도움이 되셨다니 제가 기쁘네요. 감사합니다. {topic=reset}

    + next
    - <br=2>다른 제품에 대한 정보도 알려드릴까요?
< topic
